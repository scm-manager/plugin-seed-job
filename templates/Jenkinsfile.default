#!groovy
pipeline {
  options {
    buildDiscarder(logRotator(numToKeepStr: '10'))
    disableConcurrentBuilds()
  }

  agent {
    docker {
      image 'scmmanager/java-build:17.0.9_9'
      label 'scmm'
    }
  }

  environment {
    HOME = "${env.WORKSPACE}"
    SONAR_USER_HOME = "${env.WORKSPACE}/.sonar"
  }

  stages {
    stage('Prepare build') {
      steps {
        script {
          buildTool = new Gradle(this)
        }
      }
    }
    stage('Validate core version') {
      when {
        anyOf {
          branch pattern: 'release/*', comparator: 'GLOB'
          branch pattern: 'hotfix/*', comparator: 'GLOB'
        }
      }
      steps {
        script {
          if (fileExists('package.json')) {
            def packageJson = readJSON file: 'package.json'
            def uiPluginVersion = packageJson.get("dependencies").get("@scm-manager/ui-plugins")
            if (uiPluginVersion == null) {
              uiPluginVersion = packageJson.get("devDependencies").get("@scm-manager/ui-plugins");
            }
            if (uiPluginVersion.contains("-")) {
              currentBuild.result = 'FAILURE'
              error("Invalid version of ui-plugins in package.json found: ${uiPluginVersion}. Please use official version instead of snapshot.")
            }
          }

          def propertiesFile = readFile('build.gradle')
          def lines = propertiesFile.readLines()
          lines.collect{ line ->
            if (line.contains("scmVersion") && line.contains("SNAPSHOT")) {
              currentBuild.result = 'FAILURE'
              error("Invalid core version in build.gradle found. Please use official version instead of snapshot.")
            }
            if (line.contains("sonia.scm.plugins:") && line.contains("SNAPSHOT")) {
              currentBuild.result = 'FAILURE'
              error("Invalid plugin dependency version in build.gradle found. Please use proper versions of all dependencies.")
            }
          }
        }
      }
    }
    stage('Set Version') {
      when {
        anyOf {
          branch pattern: 'release/*', comparator: 'GLOB'
          branch pattern: 'hotfix/*', comparator: 'GLOB'
        }
      }
      steps {
        sh "git tag -d ${releaseVersion} || true"
        sh "git fetch --tags"
        sh "git checkout ${env.BRANCH_NAME}"
        sh "git reset --hard origin/${env.BRANCH_NAME}"

        // fetch all remotes from origin
        sh 'git config --replace-all "remote.origin.fetch" "+refs/heads/*:refs/remotes/origin/*"'
        sh 'git fetch --all'

        // read version from brach, set it and commit it
        script {
          buildTool.setVersion(releaseVersion)
        }
        commit "release version ${releaseVersion}"


        script {
          if (isReleaseBuild()) {
            // checkout, reset and merge
            sh 'git checkout main'
            sh 'git reset --hard origin/main'
            sh "git merge --ff-only ${env.BRANCH_NAME}"
          }
        }

        // set tag
        tag releaseVersion
      }
    }

    stage('Build') {
      steps {
        script {
          buildTool.build()
        }
      }
    }

    stage('Check') {
      steps {
        script {
          buildTool.check()
        }
      }
    }

    stage('SonarQube') {
      when {
        not {
          anyOf {
            branch pattern: 'release/*', comparator: 'GLOB'
            branch pattern: 'hotfix/*', comparator: 'GLOB'
          }
        }
      }
      steps {
        sh 'git config --replace-all "remote.origin.fetch" "+refs/heads/*:refs/remotes/origin/*"'
        sh 'git fetch origin develop'
        script {
          buildTool.sonarQube()
        }
      }
    }

    stage('Deployment') {
      when {
        expression { return isBuildSuccess() }
        anyOf {
          branch pattern: 'release/*', comparator: 'GLOB'
          branch pattern: 'hotfix/*', comparator: 'GLOB'
          branch pattern: 'develop', comparator: 'GLOB'
          branch pattern: 'feature/react-19-upgrade', comparator: 'GLOB'
        }
      }
      steps {
        withYarnAuth('npm-token-scm-manager') {
          script {
            buildTool.deploy()
          }
        }
      }
      post {
        always {
          sh 'rm -f settings.xml .npmrc .yarnrc || true'
        }
      }
    }

    stage('Snapshot Deployment') {
      when {
        expression { return isBuildSuccess() }
        anyOf {
          branch pattern: 'develop', comparator: 'GLOB'
          branch pattern: 'feature/react-19-upgrade', comparator: 'GLOB'
        }
      }
      steps {
        withCredentials([
          usernamePassword(credentialsId: 'packages-scm-manager-org', usernameVariable: 'serverUsername', passwordVariable: 'serverPassword')
        ]) {
          script {
            String pluginName = env.JOB_NAME.split('/')[2]
            if (env.BRANCH_NAME == 'develop') {
              sh 'curl --fail --silent -u ${serverUsername}:${serverPassword} --upload-file ./build/libs/' + pluginName + '.smp https://packages.scm-manager.org/repository/latest_plugin_snapshots/' + pluginName + '.smp'
            } else {
              sh 'curl --fail --silent -u ${serverUsername}:${serverPassword} --upload-file ./build/libs/' + pluginName + '.smp https://packages.scm-manager.org/repository/react19_plugin_snapshots/' + pluginName + '.smp'
            }
          }
        }
      }
    }

    stage('Push Tag') {
      when {
        anyOf {
          branch pattern: 'release/*', comparator: 'GLOB'
          branch pattern: 'hotfix/*', comparator: 'GLOB'
        }
        expression { return isBuildSuccess() }
      }
      steps {
        script {
          // push changes back to remote repository
          if (isReleaseBuild()) {
            authGit 'SCM-Manager', 'push origin main --tags'
            authGit 'SCM-Manager', 'push origin --tags'
          } else {
            authGit 'SCM-Manager', "push origin ${env.BRANCH_NAME} --tags"
          }
          // push tag to GitHub
          String pluginName = env.JOB_NAME.split('/')[2]
          authGit 'cesmarvin', "push --tags https://github.com/scm-manager/${pluginName}"
        }
      }
    }

    stage('Plugin Center') {
      when {
        anyOf {
          branch pattern: 'release/*', comparator: 'GLOB'
          branch pattern: 'hotfix/*', comparator: 'GLOB'
        }
        expression { return isBuildSuccess() }
      }
      steps {
        sh 'rm -rf plugin-center || true'
        sh 'mkdir plugin-center'
        dir('plugin-center') {
          script {
            if (isReleaseBuild()) {
              authGit 'SCM-Manager', 'push origin HEAD:main'
            }
            git branch: 'master', changelog: false, poll: false, url: 'https://ecosystem.cloudogu.com/scm/repo/scm-manager/website'
            String filename = releaseVersion.replace('.', '-') + '.yaml'
            String pluginName = env.JOB_NAME.split('/')[2]
            dir("content/plugins/${pluginName}/releases") {
              sh "mv ${buildTool.releaseDescriptorPath()} ${filename}"
              sh "git add ${filename}"
              commit "${pluginName}: release ${releaseVersion}"
              authGit 'SCM-Manager', 'push origin HEAD:master'
            }
          }
        }
      }
    }

    stage('Set Next Version') {
      when {
        branch pattern: 'release/*', comparator: 'GLOB'
        expression { return isBuildSuccess() }
      }
      steps {
        sh returnStatus: true, script: 'git branch -D develop'
        sh 'git checkout develop'
        sh 'git merge main'
        script {
          buildTool.setVersionToNextSnapshot()
        }

        commit 'Prepare for next development iteration'
        authGit 'SCM-Manager', 'push origin develop'
      }
    }

    stage('Delete Release Branch') {
      when {
        branch pattern: 'release/*', comparator: 'GLOB'
        expression { return isBuildSuccess() }
      }
      steps {
        authGit 'SCM-Manager', "push origin :${env.BRANCH_NAME}"
      }
    }

    stage('Push to GitHub') {
      when {
        anyOf {
          branch pattern: 'develop', comparator: 'GLOB'
          branch pattern: 'main', comparator: 'GLOB'
          branch pattern: 'support/*', comparator: 'GLOB'
        }
        expression { return isBuildSuccess() }
      }
      steps {
        script {
          String pluginName = env.JOB_NAME.split('/')[2]
          authGit 'cesmarvin', "push -f https://github.com/scm-manager/${pluginName} HEAD:${env.BRANCH_NAME}"
        }
      }
    }

    stage('Send Merge Notification') {
      when {
        branch pattern: 'hotfix/*', comparator: 'GLOB'
        expression { return isBuildSuccess() }
      }
      steps {
        mail to: "scm-team@cloudogu.com",
          subject: "Jenkins Job ${JOB_NAME} - Merge Hotfix Release #${env.BRANCH_NAME}!",
          body: """Please,
          - merge the hotfix release branch ${env.BRANCH_NAME} into main (keep versions of main, merge changelog to keep both versions),
          - merge main into develop (the changelog should have no conflicts),
          - if needed, increase version."""
      }
    }
  }

  post {
    failure {
      mail to: 'scm-team@cloudogu.com',
        subject: "Jenkins Job ${JOB_NAME} - Build #${BUILD_NUMBER} - ${currentBuild.currentResult}!",
        body: "Check console output at ${BUILD_URL} to view the results."
    }
    unstable {
      mail to: 'scm-team@cloudogu.com',
        subject: "Jenkins Job ${JOB_NAME} - Build #${BUILD_NUMBER} - ${currentBuild.currentResult}!",
        body: "Check console output at ${BUILD_URL} to view the results."
    }
    fixed {
      mail to: 'scm-team@cloudogu.com',
        subject: "Jenkins Job ${JOB_NAME} - Is back to normal with Build #${BUILD_NUMBER}",
        body: "Check console output at ${BUILD_URL} to view the results."
    }
  }
}

BuildTool buildTool

boolean isReleaseBuild() {
  return env.BRANCH_NAME.startsWith('release/')
}

String getReleaseVersion() {
  if (isReleaseBuild()) {
    return env.BRANCH_NAME.substring("release/".length());
  } else {
    return env.BRANCH_NAME.substring("hotfix/".length());
  }
}

void commit(String message) {
  sh "git -c user.name='CES Marvin' -c user.email='cesmarvin@cloudogu.com' commit -m '${message}'"
}

void tag(String version) {
  String message = "release version ${version}"
  sh "git -c user.name='CES Marvin' -c user.email='cesmarvin@cloudogu.com' tag -m '${message}' ${version}"
}

boolean isBuildSuccess() {
  return currentBuild.result == null || currentBuild.result == 'SUCCESS'
}

void withYarnAuth(String credentials, Closure<Void> closure) {
  withCredentials([string(credentialsId: credentials, variable: 'NPM_TOKEN')]) {
    writeFile encoding: 'UTF-8', file: '.npmrc', text: "//registry.npmjs.org/:_authToken='${NPM_TOKEN}'"
    writeFile encoding: 'UTF-8', file: '.yarnrc', text: '''
      registry "https://registry.npmjs.org/"
      always-auth true
      email cesmarvin@cloudogu.com
    '''.trim()

    closure.call()
  }
}

void authGit(String credentials, String command) {
  withCredentials([
    usernamePassword(credentialsId: credentials, usernameVariable: 'AUTH_USR', passwordVariable: 'AUTH_PSW')
  ]) {
    sh "git -c credential.helper=\"!f() { echo username='\$AUTH_USR'; echo password='\$AUTH_PSW'; }; f\" ${command}"
  }
}

interface BuildTool {
  void setVersion(String version)
  void check()
  void build()
  void sonarQube()
  void deploy()
  String releaseDescriptorPath()
  void setVersionToNextSnapshot()

}

class Gradle implements BuildTool {

  def script

  Gradle(def script) {
    this.script = script
  }

  void setVersion(String version) {
    gradle "setVersion --newVersion=${version}"
    gradle 'fix'
    script.sh 'git add gradle.properties'
    try {
      script.sh 'git add package.json'
    } catch (Exception e) {
      println('Could not add package.json')
    }
  }

  void check() {
    gradle 'check'
    // update timestamp to avoid rerun tests again and fix junit-plugin:
    // ERROR: Test reports were found but none of them are new
    script.sh 'touch --no-create build/test-results/*/*.xml || true'
    script.sh 'touch --no-create build/jest-reports/TEST-*.xml || true'
    script.sh 'touch --no-create build/vite-reports/TEST-*.xml || true'
    script.junit allowEmptyResults: true, testResults: 'build/test-results/*/*.xml,build/vite-reports/TEST-*.xml,build/jest-reports/TEST-*.xml'
  }

  void build() {
    gradle 'build -xtest'
    script.archiveArtifacts artifacts: 'build/libs/*.smp'
  }

  void sonarQube() {
    script.withSonarQubeEnv('sonarcloud.io-scm') {
      String repoNamespace = script.env.JOB_NAME.split('/')[1]
      String repoName = script.env.JOB_NAME.split('/')[2]
      def status = script.sh returnStatus: true, script: "curl -f https://sonarcloud.io/api/project_branches/list?project=sonia.scm.plugins:${repoName}"
      String sonar = "sonarqube -Dsonar.organization=scm-manager -Dsonar.analysis.scmm-repo=${repoNamespace}/${repoName}"
      if (status == 0) {
        sonar += " -Dsonar.branch.name=${script.env.BRANCH_NAME}"
      }
      if (script.env.BRANCH_NAME != 'develop') {
        sonar += ' -Dsonar.branch.target=develop'
      }
      gradle sonar
    }
  }

  void deploy() {
    script.withCredentials([script.usernamePassword(credentialsId: 'packages-scm-manager-org', passwordVariable: 'serverPassword', usernameVariable: 'serverUsername')]) {
      gradle "publish -PpackagesScmManagerUsername=${script.env.serverUsername} -PpackagesScmManagerPassword=${script.env.serverPassword}"
    }
  }

  String releaseDescriptorPath() {
    return "${script.env.WORKSPACE}/build/libs/release.yaml"
  }

  void setVersionToNextSnapshot() {
    gradle 'setVersionToNextSnapshot'
    gradle 'fix'
    script.sh 'git add gradle.properties'
    try {
      script.sh 'git add package.json'
    } catch (Exception e) {
      println('Could not add package.json')
    }
  }

  void gradle(String command) {
    script.sh "./gradlew ${command}"
  }
}

